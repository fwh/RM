/* 
	filename   : RM.ssc
	created on : May 3, 2013
	description: This grammar represents the grammar of RM given at the exercise
*/

Package rm;

Helpers
    all = [0 .. 0xFFFF];
    //ASCII code for tab is 9
    tab = 9;
    //ASCII code for tab is 13
    cr = 13;
    //ASCII code for tab is 10
    lf = 10;
    //Support either lowercase or uppercase characters
    letter = [['A'..'Z'] + ['a'..'z']];	
    digit = ['0' .. '9'];
    eol = cr lf | cr | lf;   
    //These are needed for comments
    not_star = [all - '*'];
    not_star_slash = [not_star - '/'];
    short_comment = '//' [all - [cr + lf]]* eol;
    long_comment = '/*' not_star* '*'+ (not_star_slash not_star* '*'+)* '/';

/* define token */
Tokens
    blank = (' ' | eol | tab)+;
    plussym = '+';
    minussym = '-';
    timessym = '*';
    imodsym = '%';
    divsym = '/';
    lesssym  = '<';
    lseqsym = '<=';
    noteqsym = '!=';
    greqsym = '>=';
    grtrsym = '>';
    lshiftsym = '<<';
    rshiftsym = '>>';
    notsym = '!';
    
    condop = '&&'  | '||';
    ifsym = 'IF';
    thensym = 'THEN';
    elsesym = 'ELSE';
    letsym = 'LET';
    succsym = 'SUCC';
    
    lparsym = '(';
    rparsym = ')';
    lbracksym = '[';
    rbracksym = ']';
    endsym = '.';
    commasym = ',';

    semicolonsym = ';';
    assignsym = '=';
    equalsym = '==';
    questionsym = '?';
    esym = 'E';
    intdenotation = digit+;
    realdenotation = digit+ '.' digit+;
    ident = letter+;
    charsym = '''letter''';
    stringsym = '"'letter*'"';
    apostrophe = ''';
    comment = short_comment | long_comment;

/* Token to be ignored */
Ignored Tokens
    comment, blank;

Productions
    program = {def} def program | 
              {comp} comp program |  
              {end} endsym;
    def = letsym ident parlst assignsym expr semicolonsym;
    comp = expr questionsym;
    parlst = {parlst} lparsym pars rparsym | 
             {empty} empty;
    pars = {ident} ident | 
           {comma} pars commasym ident;
    expr = {complex} ifsym relcomp thensym [trueclause]:expr elsesym [falseclause]:expr | 
           {simple} simpleexpr;
    //Left recursion is no problem here
    relcomp = {compcond} [p1]:relcomp condop [p2]:relexpr | 
              {cond} relexpr;
    relexpr = [left]:expr relop [right]:expr;
    simpleexpr = {term}term 
             | {add}simpleexpr addop term;
    term = {factor}factorexpr | 
           {mult}term mulop factorexpr;
    factorexpr = {par}lparsym expr rparsym 
               | {simplefac} simplefactor;
    simplefactor  = 
               {num}numfac
             | {char} charsym
             | {string} stringsym 
             | {ident}ident arglst;
    numfac = {int}intdenotation | 
             {real} realnum | 
             {monad} monadexpr;
    realnum = {scnum} [base]:realdenotation esym [exp]:numfac | 
              {real}realdenotation;
    monadexpr = {neg} minussym factorexpr |
                {succchar} succsym simplefactor;
    arglst = {args}lparsym args rparsym | 
             {empty}empty;
    args = {expr}expr | 
           {listargs}args commasym expr;
    addop = {plus}plussym | 
            {minus}minussym;
    mulop =   {mult}timessym 
            | {div}divsym 
            | {integermod}imodsym 
            | {lshift} lshiftsym
            | {rshift} rshiftsym;
    relop =   {lessthan}lesssym 
            | {lessorequal}lseqsym 
            | {equal}equalsym 
            | {notequal}noteqsym 
            | {greaterequal}greqsym 
            | {greater}grtrsym;
    empty = ;